export LANG=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export TERM=xterm-256color

export XDG_CONFIG_HOME="$HOME/.config"
export EDITOR="nvim"

# Source personal configs
PERSONAL="$XDG_CONFIG_HOME/personal"
if [[ -d "$PERSONAL" ]]; then
  for f in "$PERSONAL"/*; do
    [[ -f "$f" ]] && source "$f"
  done
fi

# FZF
fzf_dir="$HOME/personal/fzf"
if [[ -d "$fzf_dir" ]]; then
  [[ ":$PATH:" != *":$fzf_dir/bin:"* ]] && export PATH="$PATH:$fzf_dir/bin"
  [[ -f "$fzf_dir/shell/key-bindings.zsh" ]] && source "$fzf_dir/shell/key-bindings.zsh"
  [[ -f "$fzf_dir/shell/completion.zsh" ]] && source "$fzf_dir/shell/completion.zsh"
fi

# fd for fzf (faster than find)
if command -v fd &>/dev/null; then
  export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi

# direnv - auto-load .envrc files
if command -v direnv &>/dev/null; then
  eval "$(direnv hook zsh)"
fi

# Navigation - word movement
autoload -U select-word-style
select-word-style shell

bindkey "^[[1;3D" backward-word
bindkey "^[[1;3C" forward-word

# Path-based word movement
backward-slash-word() {
  while [[ -n $LBUFFER && $LBUFFER != */ ]]; do
    zle backward-char
  done
  [[ $LBUFFER == */ ]] && zle backward-char
}
zle -N backward-slash-word

forward-slash-word() {
  while [[ -n $RBUFFER && $RBUFFER != /* ]]; do
    zle forward-char
  done
  [[ $RBUFFER == /* ]] && zle forward-char
}
zle -N forward-slash-word

bindkey "^[[1;4D" backward-slash-word
bindkey "^[[1;4C" forward-slash-word
